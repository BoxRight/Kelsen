<declaration> ::= <type> <identifier> ('=' <expression>)? ';'
<type> ::= 'int' | 'float' | 'string' | 'asset' | 'subject'

<identifier> ::= [a-zA-Z_][a-zA-Z0-9_]*

<expression> ::= <additive_expression> 
               | <asset_expression> 
               | <clause_expression> 
               | <subject_expression> 
               | <string_expression> 
               | <legal_expression> 
               | <conditional_expression> 
               | <legal_query_expression>

<string_expression> ::= '"' [^"]* '"'
<additive_expression> ::= <term> (('add' | 'sub') <term>)*
<asset_expression> ::= <asset_type> ',' <asset_subtype> ',' <subject_expression> ',' <string_expression> ',' <subject_expression> ';'
<subject_expression> ::= <string_expression> ',' <string_expression> ',' <number> ',' <string_expression> ';'

<legal_expression> ::= <claim-right> 
					| <obligation> 
					| <prohibition> 
					| <privilege>
					
<claim-right> ::= 'CR(' <asset_expression> ')'
<obligation> ::= 'OB(' <asset_expression> ')'
<prohibition> ::= 'PR(' <asset_expression> ')'
<privilege> ::= 'PVG(' <asset_expression> ')'

<conditional_expression> ::= 'condition:' <asset_expression> ',' 'consequence:' <legal_expression> ';'

<clause_expression> ::= 'Clause' <identifier> '{' <conditional_expression> '}' ';'

<legal_query_expression> ::= 'Â¿' <identifier> <legal_meta_expression> '?' ';'

<legal_meta_expression> ::= <power> 
						| <liability> 
						| <disability> 
						| <immunity>
						
<power> ::= 'PWR(' <asset_expression> ',' <asset_expression> ',' <legal_expression> ')'
<liability> ::= 'LIAB('  <asset_expression> ',' <asset_expression> ',' <legal_expression> ')'
<disability> ::= 'DIS(' <asset_expression> ',' <asset_expression> ',' <legal_expression> ')'
<immunity> ::= 'IMM(' <asset_expression> ',' <asset_expression> ',' <legal_expression> ')'

<asset_type> ::= 'Service' | 'Property'
<asset_subtype> ::= 'NM' | 'M' | '+' | '-'

<term> ::= <factor> (('*' | '/') <factor>)*
<factor> ::= <number> | <identifier> | '(' <expression> ')'
<number> ::= [0-9]+



